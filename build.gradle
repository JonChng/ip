plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
}

checkstyle {
    toolVersion = '11.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

spotless {
    java {
        // Reorder + group imports (matches SE-EDU convention)
        importOrder 'java', 'javax', 'org', 'com', ''    // '' = everything else (your packages)
        removeUnusedImports()
        // Google Java Format for indentation/spacing
        googleJavaFormat('1.17.0')
        // Ensure newline at EOF etc.
        endWithNewline()
        trimTrailingWhitespace()
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("jeff.ui.Jeff")
}

shadowJar {
    archiveBaseName = "jeff"
    archiveVersion = ""
    archiveClassifier = null
}

run{
    standardInput = System.in
}


